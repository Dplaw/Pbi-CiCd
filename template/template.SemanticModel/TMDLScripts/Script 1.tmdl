createOrReplace

	table _Measures
		lineageTag: 49fe2eaa-0f1b-4d69-a417-eb8a943a50d5

		measure 'Sales USD' =
				
				CALCULATE(
				    SUM(Sales_fct[Sales in USD]),
				    KEEPFILTERS(Client_dim))
			formatString: #,0.00
			displayFolder: Sales
			lineageTag: 18973c8f-bacd-4ae0-9cc3-0ca5784aad21

		measure 'Sales USD PY' =
				
				CALCULATE(
				    SUM(Sales_fct[Sales in USD]),
				    SAMEPERIODLASTYEAR('Calendar'[Date])
				)
			formatString: #,0.00
			displayFolder: Sales
			lineageTag: 21c48163-9d3c-4b75-8582-c5cfa08fd37a

		measure 'Sales Units' = ```
				
				    SUM(Sales_fct[Sales in Units])
				```
			formatString: 0.00
			displayFolder: Sales
			lineageTag: 22649d56-3a07-4261-9edf-bfaff1100696

		measure 'Sales Units PY' =
				
				
				CALCULATE(
				    [Sales Units],
				    SAMEPERIODLASTYEAR('Calendar'[Date])
				)
			displayFolder: Sales
			lineageTag: c155dcdb-611e-458d-a229-d3194a921f5e

			annotation PBI_FormatHint = {"isGeneralNumber":true}

		measure 'PY Δ USD' =
				
				DIVIDE([Sales USD] - [Sales USD PY], [Sales USD])
			formatString: 0.00%;-0.00%;0.00%
			displayFolder: Sales
			lineageTag: 3415c35c-c70f-414a-b1ce-509c216d7fa3

		measure 'Target USD' =
				
				SUM('Targets Fy25_fct'[Sales Target in USD])
			displayFolder: Target
			lineageTag: d8878b79-47c5-4cb1-90f5-c28e3e780a24

			annotation PBI_FormatHint = {"isGeneralNumber":true}

		measure 'Latest Target' = ```
				
				VAR MaxDateTarget = CALCULATE ( MAX( 'Targets Fy25_fct'[FyEnd] ), REMOVEFILTERS( 'Calendar' ))
				RETURN
				CALCULATE(
				    [Target USD], 
				    'Calendar'[Date] = MaxDateTarget
				)
				```
			displayFolder: Target
			lineageTag: c0076aec-4b8f-4853-82c5-e9670d8df14b

			annotation PBI_FormatHint = {"isGeneralNumber":true}

		measure '% Target' =
				
				IF ([Target USD],
				    DIVIDE(
				        [Target USD] ,
				        [Sales USD]
				    ))
			formatString: 0.00%;-0.00%;0.00%
			displayFolder: Target
			lineageTag: 05ec487b-8979-4c86-84b8-bde63577ebb0

		measure 'Title Line Chart' =
				
				VAR sel = SELECTEDVALUE( 'USD vs UNIT'[Value4])
				RETURN "CY Sales vs PY in " & sel
			displayFolder: Titles
			lineageTag: c5c8f1c1-3c71-4577-a858-b0dcbcf4fd2a

		measure 'PY Δ USD Title' =
				
				CONCATENATE("SALES USD                                   " & UNICHAR(10)&UNICHAR(10), FORMAT([Sales USD], "Currency"))
			displayFolder: Titles
			lineageTag: 9bd34732-4585-4e33-b08d-dfb3aedaf386

		measure Diff = [Sales USD] - [Target USD]
			displayFolder: Target
			lineageTag: 1ece7055-558d-4822-8c3c-702e491881c5

			annotation PBI_FormatHint = {"isGeneralNumber":true}

		measure Status = ```
				
				VAR Raw = [% Target]
				VAR Pct =
				    VAR p = IF(Raw > 1, Raw / 100.0, Raw)
				    RETURN MAX(0, MIN(1, p))
				VAR W = 100
				VAR H = 8
				VAR Fill =
				    SWITCH(TRUE(),
				        Pct < 0.5, "#C0392B",   
				        Pct < 0.85, "#D4AC0D",  
				        "#1E8449"               
				    )
				VAR BarW = INT(Pct * W)
				RETURN
				"data:image/svg+xml;utf8," &
				"<svg xmlns='http://www.w3.org/2000/svg' viewBox='0 0 " & W & " " & H &
				"' width='" & W & "' height='" & H &
				"' preserveAspectRatio='none' style='display:block'>" &
				  "<rect x='0' y='0' width='" & W & "' height='" & H & "' rx='2' fill='#E0E0E0'/>" &
				  "<rect x='0' y='0' width='" & BarW & "' height='" & H & "' rx='2' fill='" & Fill & "'/>" &
				  "<text x='" & (W/2) & "' y='" & (H/2) &
				  "' font-size='6' font-family='Segoe UI' fill='#fff' text-anchor='middle' dominant-baseline='middle'>" &
				     FORMAT(Pct, "0%") &
				  "</text>" &
				"</svg>"
				```
			displayFolder: Target
			lineageTag: 20da8698-03c9-42f4-8a89-b0570184fabe
			dataCategory: ImageUrl

		measure 'Conditional Sales' =
				
				IF( [Target USD], [Sales USD])
			displayFolder: Target
			lineageTag: 24834f9e-dcee-4fce-a1da-0db7d3c59e54

			annotation PBI_FormatHint = {"isGeneralNumber":true}

		measure 'RT Dynamic' = ```
				
				VAR Sel =
				    SELECTEDVALUE ( 'CountryBuCli'[Parameter Fields] )
				VAR UnitSel =
				    SELECTEDVALUE ( 'USD vs UNIT'[Value4] )
				RETURN
				    SWITCH (
				        TRUE (),
				        Sel = "'Client_dim'[Business Unit]",
				            VAR CurIdx =
				                MAX ( 'Client_dim'[Business Unit] )
				            RETURN
				                CALCULATE (
				                    SWITCH ( UnitSel, "USD", [Sales USD], "Units", [Sales Units], BLANK () ),
				                    FILTER (
				                        ALLSELECTED ( 'Client_dim'[Business Unit] ),
				                        'Client_dim'[Business Unit] <= CurIdx
				                    )
				                ),
				        Sel = "'location_dim'[Region]",
				            VAR CurIdx =
				                MAX ( 'location_dim'[Region] )
				            RETURN
				                CALCULATE (
				                    SWITCH ( UnitSel, "USD", [Sales USD], "Units", [Sales Units], BLANK () ),
				                    FILTER (
				                        ALLSELECTED ( 'location_dim'[Region] ),
				                        'location_dim'[Region] <= CurIdx
				                    )
				                )
				    )
				
				```
			displayFolder: Sales
			lineageTag: 3bef4806-5ed8-4c38-8a2f-274a936ebb7e

			annotation PBI_FormatHint = {"isGeneralNumber":true}

		measure 'Sales Dynamic' = ```
				
				VAR UnitSel =
				    SELECTEDVALUE ( 'USD vs UNIT'[Value4] )
				RETURN
				SWITCH ( UnitSel, "USD", [Sales USD], "Units", [Sales Units], BLANK () )
				
				```
			displayFolder: Sales
			lineageTag: 9622b0c7-136a-4f8e-b0c6-70286831eaef

			annotation PBI_FormatHint = {"isGeneralNumber":true}

		measure TargetIfUsd =
				
				
				VAR UnitSel =
				    SELECTEDVALUE ( 'USD vs UNIT'[Value4] )
				RETURN
				IF ( UnitSel = "USD", CALCULATE([Latest Target], ALL() )
				)
			displayFolder: Sales
			lineageTag: c4e691b0-474b-402c-a2e7-61e5547985f6

			annotation PBI_FormatHint = {"isGeneralNumber":true}

		measure 'Title Bar chart' = ```
				
				VAR UsdUnits = SELECTEDVALUE( 'USD vs UNIT'[Value4] )
				VAR RegionBu = SELECTEDVALUE( 'CountryBuCli'[Value4])
				RETURN 
				"Sales & Cumulative sales by " & RegionBu & " in " & UsdUnits
				```
			displayFolder: Titles
			lineageTag: fb13dcf2-04c9-4ea7-9e70-668e06cde399

		measure 'RT Customer usd' =
				
				VAR CurIdx = MAX ( 'Client_dim'[Client ID] )
				
				RETURN
				    CALCULATE (
				        SUM(Sales_fct[Sales in USD]),
				        'Client_dim'[Client ID] <= CurIdx,
				        ALLSELECTED( Client_dim[Client ID])
				)
			displayFolder: Sales
			lineageTag: 8dcb5ffd-bebb-4975-a069-768571060d93

			annotation PBI_FormatHint = {"isGeneralNumber":true}

		measure 'RT Customer units' =
				
				VAR CurIdx = MAX ( 'Client_dim'[Client ID] )
				
				RETURN
				    CALCULATE (
				        SUM(Sales_fct[Sales in Units]),
				        'Client_dim'[Client ID] <= CurIdx,
				        ALLSELECTED( Client_dim[Client ID])
				)
			formatString: 0
			displayFolder: Sales
			lineageTag: 90b419b4-15d4-43f2-a506-a6f4a3361915

		partition _Measures = m
			mode: import
			source =
					let
					    Source = Table.FromRows(Json.Document(Binary.Decompress(Binary.FromText("i44FAA==", BinaryEncoding.Base64), Compression.Deflate)), let _t = ((type nullable text) meta [Serialized.Text = true]) in type table [Column1 = _t]),
					    #"Changed Type" = Table.TransformColumnTypes(Source,{{"Column1", type text}}),
					    #"Removed Columns" = Table.RemoveColumns(#"Changed Type",{"Column1"})
					in
					    #"Removed Columns"

		annotation PBI_ResultType = Table

